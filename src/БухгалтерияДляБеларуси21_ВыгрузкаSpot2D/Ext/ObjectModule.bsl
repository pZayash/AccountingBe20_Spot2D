Перем мНастройкаКомпоновкиСпискаНоменклатуры;
Перем мСхемаКомпоновкиСпискаНоменклатуры;

#Область ВнешнийИнтефейс 

Процедура ВыполнитьКоманду(ИдентификаторКоманды = "Выгрузить", ПараметрыКоманды = Неопределено) Экспорт
	
	Инициализировать();
	
	Если ИдентификаторКоманды = ИмяКоманды_ВыгрузитьВФоне() Тогда 

		// Спец настройки для фоновой выгрузки
		ПериодВыгрузки_Установить();
		ВыгрузитьВсе();

	ИначеЕсли ИдентификаторКоманды = "Выгрузить" Тогда
		ВыгрузитьВсе();
		
	КонецЕсли; 

КонецПроцедуры // ВыполнитьКоманду

#КонецОбласти // ВнешнийИнтерфейс

#Область СлужебныйИнтерфейс

Процедура ВосстановитьНастройки() Экспорт
	
	// Восстановить сохраненные настройки
	структураНастроек = Новый Структура;
	структураНастроек = ОбщегоНазначения.ХранилищеНастроекДанныхФормЗагрузить(КлючОбъекта(), КлючНастроек(), структураНастроек, , "");
	
	массивИсключенныйСвойств = Новый Массив;
	
	стрИсключаяСвойства = СтрСоединить(массивИсключенныйСвойств, ",");
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, структураНастроек, , стрИсключаяСвойства); 
	
	Если структураНастроек.Свойство("СхемаКомпоновкиСпискаНоменклатуры") Тогда
		мСхемаКомпоновкиСпискаНоменклатуры = структураНастроек.СхемаКомпоновкиСпискаНоменклатуры;
		
	КонецЕсли;
	
	Если структураНастроек.Свойство("НастройкаКомпоновкиСпискаНоменклатуры") Тогда
		мНастройкаКомпоновкиСпискаНоменклатуры = структураНастроек.НастройкаКомпоновкиСпискаНоменклатуры;
		
	КонецЕсли;

КонецПроцедуры

Процедура Инициализировать() Экспорт 
	
	ФорматнаяСтрокаДат = "ДФ=yyyy-MM-dd"; 
	ВерсияОбъекта = СведенияОВнешнейОбработке().Версия;
	
	списокИменФайлов = Новый СписокЗначений;
	списокИменФайлов.Добавить(ИмяФайла_delivery());
	списокИменФайлов.Добавить(ИмяФайла_stocks());
	списокИменФайлов.Добавить(ИмяФайла_ttoptions());
	// списокИменФайлов.Добавить(ИмяФайла_ta()); // Не реализовано
	списокИменФайлов.Добавить(ИмяФайла_sku());
	списокИменФайлов.Добавить(ИмяФайла_receive());
	списокИменФайлов.Добавить(ИмяФайла_cancellations()); // Не реализовано
		
	Очередь.Очистить(); 
	
	Для Каждого элементСписка Из списокИменФайлов Цикл
		рядОчереди = Очередь.Добавить();
		рядОчереди.ИмяФайла = элементСписка.Значение;
		
	КонецЦикла;
	элементСписка = Неопределено; 
	
	// Настройки по умолчанию
	ПериодВыгрузки_Установить();
	
	ВосстановитьНастройки();
	
КонецПроцедуры 

Процедура ОтборКомпоновкиСпискаНоменклатуры_Заполнить(пОтборПриемник) Экспорт
	
	Если ТипЗнч(мНастройкаКомпоновкиСпискаНоменклатуры) = Тип("НастройкиКомпоновкиДанных") Тогда
		ОтборСКД_Копировать(пОтборПриемник, мНастройкаКомпоновкиСпискаНоменклатуры.Отбор); 
		
	КонецЕсли;

КонецПроцедуры

Процедура СоздатьСодержимоеФайлов() Экспорт 
	
	ВосстановитьНастройки();
	
	запрос = Новый Запрос;
	запрос.УстановитьПараметр("ID_дистрибьютора", ID_дистрибьютора);
	запрос.УстановитьПараметр("ДатаНачала", НачалоДня(ПериодВыгрузки.ДатаНачала));
	запрос.УстановитьПараметр("ДатаОкончания", КонецДня(ПериодВыгрузки.ДатаОкончания)); // Конец прошлого дня
	запрос.УстановитьПараметр("Счет41_1", ПланыСчетов.Хозрасчетный.ТоварыНаСкладах);
	мвт = Новый МенеджерВременныхТаблиц;
	запрос.МенеджерВременныхТаблиц = мвт;
	
	// Инициализировать вспомогательные временные таблицы
	тзОтборНоменклатуры = СКД_в_ТЗ(мСхемаКомпоновкиСпискаНоменклатуры, мНастройкаКомпоновкиСпискаНоменклатуры);
	запрос.УстановитьПараметр("тзОтборНоменклатуры", тзОтборНоменклатуры); 

	// Создать последовательность дат циклом, чтобы не городить создание последовательности запросом
	тзДатыПоследовательность = Новый ТаблицаЗначений;
	тзДатыПоследовательность.Колонки.Добавить("Дата", Новый ОписаниеТипов("Дата"));
	дата = КонецДня(запрос.Параметры.ДатаНачала);
	Пока дата <= запрос.Параметры.ДатаОкончания Цикл
		рядТзДаты = тзДатыПоследовательность.Добавить();
		рядТзДаты.Дата = НачалоДня(дата);
		дата = КонецДня(дата + 3600*24); // Добавить день к дате
		
	КонецЦикла;
	дата = Неопределено;
	
	запрос.УстановитьПараметр("тзДатыПоследовательность", тзДатыПоследовательность);

	запрос.Текст = 
	"ВЫБРАТЬ
	|	ВЫРАЗИТЬ(т.Ссылка КАК Справочник.Номенклатура) КАК Ссылка
	|ПОМЕСТИТЬ отборНоменклатуры
	|ИЗ
	|	&тзОтборНоменклатуры КАК т
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ВЫРАЗИТЬ(т.Ссылка КАК Справочник.Номенклатура)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВЫРАЗИТЬ(д.Дата КАК ДАТА) КАК Дата
	|ПОМЕСТИТЬ датыПоследовательность
	|ИЗ
	|	&тзДатыПоследовательность КАК д
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ВЫРАЗИТЬ(д.Дата КАК ДАТА)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	отборНоменклатуры.Ссылка КАК Номенклатура,
	|	отборНоменклатуры.Ссылка.Код КАК Код_продукта_дистрибьютора,
	|	НАЧАЛОПЕРИОДА(даты.Дата, ДЕНЬ) КАК Дата
	|ПОМЕСТИТЬ матрица
	|ИЗ
	|	отборНоменклатуры КАК отборНоменклатуры
	|		ЛЕВОЕ СОЕДИНЕНИЕ датыПоследовательность КАК даты
	|		ПО (ИСТИНА)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура,
	|	Дата
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МИНИМУМ(матрица.Код_продукта_дистрибьютора) КАК Код_продукта_дистрибьютора,
	|	матрица.Дата КАК Дата
	|ПОМЕСТИТЬ перваяНоменклатура
	|ИЗ
	|	матрица КАК матрица
	|
	|СГРУППИРОВАТЬ ПО
	|	матрица.Дата
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	матрица.Дата
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Обороты.Регистратор КАК Ссылка,
	|	Обороты.КорСчет КАК КорСчет
	|ПОМЕСТИТЬ регистраторы
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Обороты(
	|			&ДатаНачала,
	|			&ДатаОкончания,
	|			Регистратор,
	|			Счет = &Счет41_1,
	|			ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.Номенклатура),
	|			Субконто1 В
	|				(ВЫБРАТЬ
	|					о.Ссылка
	|				ИЗ
	|					отборНоменклатуры КАК о),
	|			,
	|			) КАК Обороты
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РТУ.Ссылка КАК Ссылка,
	|	РТУ.Дата КАК Дата,
	|	РТУ.Контрагент КАК Клиент,
	|	РТУ.Контрагент.НаименованиеПолное КАК Название_клиента,
	|	РТУ.Контрагент.ИНН КАК Код_ИНН,
	|	РТУ.Контрагент.Код КАК КодКлиента,
	|	РТУ.Контрагент.Код КАК КодТТ,
	|	ВЫРАЗИТЬ(РТУ.ПунктРазгрузки КАК СТРОКА(256)) КАК Адрес_ТТ,
	|	РТУ.СерияБланка.Код + РТУ.НомерНакладной КАК Номер_расходной_накладной,
	|	ВЫРАЗИТЬ(ЕСТЬNULL(ЮрАдрес.Представление, """") КАК СТРОКА(256)) КАК Адрес_клиента
	|ПОМЕСТИТЬ документыРеализации
	|ИЗ
	|	Документ.РеализацияТоваровУслуг КАК РТУ
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Контрагенты.КонтактнаяИнформация КАК ЮрАдрес
	|		ПО РТУ.Контрагент = ЮрАдрес.Ссылка
	|			И (ЮрАдрес.Тип = ЗНАЧЕНИЕ(Перечисление.ТипыКонтактнойИнформации.Адрес))
	|			И (ЮрАдрес.Вид = ЗНАЧЕНИЕ(Справочник.ВидыКонтактнойИнформации.ЮрАдресКонтрагента))
	|ГДЕ
	|	РТУ.Ссылка В
	|			(ВЫБРАТЬ
	|				регистраторы.Ссылка
	|			ИЗ
	|				регистраторы)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Перемещения.Ссылка,
	|	Перемещения.Дата,
	|	Перемещения.Организация,
	|	""Розничный магазин"",
	|	Перемещения.Организация.ИНН,
	|	Перемещения.Организация.ИНН,
	|	Перемещения.СкладПолучатель.Код,
	|	ВЫРАЗИТЬ(Перемещения.ПунктРазгрузки КАК СТРОКА(256)),
	|	Перемещения.СерияБланка.Код + Перемещения.НомерНакладной,
	|	ВЫРАЗИТЬ(ЕСТЬNULL(ЮрАдрес.Представление, """") КАК СТРОКА(256))
	|ИЗ
	|	Документ.ПеремещениеТоваров КАК Перемещения
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Организации.КонтактнаяИнформация КАК ЮрАдрес
	|		ПО Перемещения.Организация = ЮрАдрес.Ссылка
	|			И (ЮрАдрес.Тип = ЗНАЧЕНИЕ(Перечисление.ТипыКонтактнойИнформации.Адрес))
	|			И (ЮрАдрес.Вид = ЗНАЧЕНИЕ(Справочник.ВидыКонтактнойИнформации.ЮрАдресКонтрагента))
	|ГДЕ
	|	Перемещения.Ссылка В
	|			(ВЫБРАТЬ
	|				регистраторы.Ссылка
	|			ИЗ
	|				регистраторы
	|			ГДЕ
	|				регистраторы.КорСчет <> &Счет41_1)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Возвраты.Ссылка,
	|	Возвраты.Дата,
	|	Возвраты.Организация,
	|	""Розничный магазин"",
	|	Возвраты.Организация.ИНН,
	|	Возвраты.Организация.ИНН,
	|	Возвраты.Склад.Код,
	|	"""",
	|	Возвраты.СервисСерияБСО + Возвраты.СервисНомерБСО,
	|	ВЫРАЗИТЬ(ЕСТЬNULL(ЮрАдрес.Представление, """") КАК СТРОКА(256))
	|ИЗ
	|	Документ.ВозвратТоваровОтПокупателя КАК Возвраты
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Организации.КонтактнаяИнформация КАК ЮрАдрес
	|		ПО Возвраты.Организация = ЮрАдрес.Ссылка
	|			И (ЮрАдрес.Тип = ЗНАЧЕНИЕ(Перечисление.ТипыКонтактнойИнформации.Адрес))
	|			И (ЮрАдрес.Вид = ЗНАЧЕНИЕ(Справочник.ВидыКонтактнойИнформации.ЮрАдресКонтрагента))
	|ГДЕ
	|	Возвраты.Ссылка В
	|			(ВЫБРАТЬ
	|				регистраторы.Ссылка
	|			ИЗ
	|				регистраторы
	|			ГДЕ
	|				регистраторы.КорСчет <> &Счет41_1)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Клиент
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Товары.Ссылка КАК Ссылка,
	|	Товары.Номенклатура КАК Номенклатура,
	|	Товары.Номенклатура.Код КАК Код_продукта_дистрибьютора,
	|	Товары.Количество КАК Количество,
	|	Товары.Сумма КАК Сумма_отгрузки,
	|	Товары.Сумма + Товары.СуммаНДС КАК Сумма_отгрузки_с_НДС
	|ПОМЕСТИТЬ реализованныеТовары
	|ИЗ
	|	Документ.РеализацияТоваровУслуг.Товары КАК Товары
	|ГДЕ
	|	Товары.Ссылка В
	|			(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|				д.Ссылка
	|			ИЗ
	|				документыРеализации КАК д)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Товары.Ссылка,
	|	Товары.Номенклатура,
	|	Товары.Номенклатура.Код,
	|	Товары.Количество,
	|	ВЫРАЗИТЬ(Товары.Цена * Товары.Количество КАК ЧИСЛО(15, 2)),
	|	Товары.СуммаВРознице
	|ИЗ
	|	Документ.ПеремещениеТоваров.Товары КАК Товары
	|ГДЕ
	|	Товары.Ссылка В
	|			(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|				д.Ссылка
	|			ИЗ
	|				документыРеализации КАК д)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Товары.Ссылка,
	|	Товары.Номенклатура,
	|	Товары.Номенклатура.Код,
	|	-Товары.Количество,
	|	-Товары.Сумма + Товары.СуммаНДС,
	|	-Товары.Сумма
	|ИЗ
	|	Документ.ВозвратТоваровОтПокупателя.Товары КАК Товары
	|ГДЕ
	|	Товары.Ссылка В
	|			(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|				д.Ссылка
	|			ИЗ
	|				документыРеализации КАК д)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	документыРеализации.Клиент КАК Клиент,
	|	документыРеализации.КодТТ КАК КодТТ,
	|	ВЫРАЗИТЬ(документыРеализации.КодКлиента + ""#"" + документыРеализации.КодТТ КАК СТРОКА(128)) КАК Код_клиента_ERP,
	|	документыРеализации.Код_ИНН КАК Код_ИНН,
	|	ВЫРАЗИТЬ(документыРеализации.Название_клиента КАК СТРОКА(128)) КАК Название_клиента,
	|	ВЫРАЗИТЬ(документыРеализации.Адрес_клиента КАК СТРОКА(256)) КАК Адрес_клиента
	|ПОМЕСТИТЬ клиенты
	|ИЗ
	|	документыРеализации КАК документыРеализации
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Контрагенты.КонтактнаяИнформация КАК ЮрАдрес
	|		ПО документыРеализации.Клиент = ЮрАдрес.Ссылка
	|			И (ЮрАдрес.Тип = ЗНАЧЕНИЕ(Перечисление.ТипыКонтактнойИнформации.Адрес))
	|			И (ЮрАдрес.Вид = ЗНАЧЕНИЕ(Справочник.ВидыКонтактнойИнформации.ЮрАдресКонтрагента))
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Клиент,
	|	КодТТ"; 
	
	запрос.Выполнить(); // Инициализация временных таблиц
	
	// Создать содержимое
	Для Каждого рядОчереди Из Очередь Цикл 
		запрос.Текст = ТекстЗапроса(рядОчереди.ИмяФайла); 
		тз = запрос.Выполнить().Выгрузить();
		
		параметрыCSV = Новый Структура;
		параметрыCSV.Вставить("ФорматДат", ФорматнаяСтрокаДат);
		параметрыCSV.Вставить("ВосстановитьПробелыВИменахКолонок", Истина);
		стрCSV = ТЗ_в_CSV(тз, , параметрыCSV);
		рядОчереди.СодержимоеФайла = стрCSV;
		
	КонецЦикла; 
	рядОчереди = Неопределено;	
		
КонецПроцедуры

Процедура СохранитьНастройки(пЭлементФормыДинамическийСписок = Неопределено) Экспорт
	
	массивИменСохраняемыхРеквизитов = Новый Массив;
	массивИменСохраняемыхРеквизитов.Добавить(ЭтотОбъект.Метаданные().Реквизиты.ID_дистрибьютора.Имя);
	массивИменСохраняемыхРеквизитов.Добавить(ЭтотОбъект.Метаданные().Реквизиты.Пользователь.Имя);
	массивИменСохраняемыхРеквизитов.Добавить(ЭтотОбъект.Метаданные().Реквизиты.Пароль.Имя);
	
	структураНастроек = Новый Структура;
	
	Для Каждого имяСохраняемогоРеквизита Из массивИменСохраняемыхРеквизитов Цикл
		значениеРеквизита = ЭтотОбъект[имяСохраняемогоРеквизита];
		структураНастроек.Вставить(имяСохраняемогоРеквизита, ЭтотОбъект[имяСохраняемогоРеквизита]);
		
	КонецЦикла;
	имяСохраняемогоРеквизита = Неопределено;
	
	// Отдельно сохранить настройки компоновщика
	Если ТипЗнч(пЭлементФормыДинамическийСписок) = Тип("ТаблицаФормы") Тогда
		структураНастроек.Вставить("НастройкаКомпоновкиСпискаНоменклатуры", пЭлементФормыДинамическийСписок.ПолучитьИсполняемыеНастройкиКомпоновкиДанных());
		структураНастроек.Вставить("СхемаКомпоновкиСпискаНоменклатуры", пЭлементФормыДинамическийСписок.ПолучитьИсполняемуюСхемуКомпоновкиДанных());
		
	КонецЕсли;

	ОбщегоНазначения.ХранилищеНастроекДанныхФормСохранить(КлючОбъекта(), КлючНастроек(), структураНастроек, , ""); 

КонецПроцедуры  

#КонецОбласти // СлужебныйИнтерфейс 

#Область СлужебныйИнтерфейсВнешнейОбработки 

Функция СведенияОВнешнейОбработке() Экспорт 
	
	параметрыРегистрации = ДополнительныеОтчетыИОбработки.СведенияОВнешнейОбработке();
	
	// Чтобы синтаксический контроль не ругался на метод Метаданные()
	// Получить его через "Выполнить"
	наименование = Неопределено;
	версия = Неопределено;
	Выполнить 
	"наименование = ЭтотОбъект.Метаданные().Синоним;
	|версия = ЭтотОбъект.Метаданные().Комментарий";
	параметрыРегистрации.Наименование = наименование;
	параметрыРегистрации.БезопасныйРежим = Ложь;
	параметрыРегистрации.Версия = версия;
	
	вид = ДополнительныеОтчетыИОбработкиКлиентСервер.ВидОбработкиДополнительнаяОбработка();
	параметрыРегистрации.Вид = вид;
	параметрыРегистрации.Информация = СтрШаблон("%1 %2", вид, наименование);
	
	ДобавитьКоманду(параметрыРегистрации.Команды, наименование, "ОткрытьОбработку", "ОткрытиеФормы", Истина); 
	ДобавитьКоманду(параметрыРегистрации.Команды, наименование + " (в фоне)", ИмяКоманды_ВыгрузитьВФоне(), "ВызовСерверногоМетода", Истина);
	
	Возврат параметрыРегистрации; 
	
КонецФункции // СведенияОВнешнейОбработке  

Процедура ДобавитьКоманду(ТаблицаКоманд, Представление, Идентификатор, Использование = "ОткрытиеФормы", ПоказыватьОповещение = Ложь, Модификатор = "ПечатьMXL")
	
	// Добавляем команду в таблицу команд по переданному описанию.
	// Параметры и их значения можно посмотреть в функции ПолучитьТаблицуКоманд
	НоваяКоманда = ТаблицаКоманд.Добавить();
	НоваяКоманда.Представление = Представление;
	НоваяКоманда.Идентификатор = Идентификатор;
	НоваяКоманда.Использование = Использование;
	НоваяКоманда.ПоказыватьОповещение = ПоказыватьОповещение;
	НоваяКоманда.Модификатор = Модификатор;
	
КонецПроцедуры 

#КонецОбласти // СлужебныйИнтерфейсВнешнейОбработки 

#Область Выгрузка

Процедура ВыгрузитьВсе() 
	
	УстановитьПривилегированныйРежим(Истина); 
	
	ВЖурнал("Выгрузка", "Начало выгрузки");

	Если Не ПроверкиПередВыгрузкой() Тогда
		Перейти ~ВыгрузитьВсе_Конец;
		
	КонецЕсли; 
	
	СоздатьСодержимоеФайлов();
	
	// Отправить файлы
	Для Каждого рядОчереди Из Очередь Цикл
		ОтправитьВSpot2D(рядОчереди.ИмяФайла, рядОчереди.СодержимоеФайла);
		
	КонецЦикла;
	рядОчереди = Неопределено;
	
	~ВыгрузитьВсе_Конец:
	
	ВЖурнал("Выгрузка", "Завершение выгрузки"); 
		
КонецПроцедуры // Выгрузка 

Функция ПроверкиПередВыгрузкой() 
	
	этоФоновое = ЭтоФоновоеЗадание();
	
	коллекцияОшибок = Новый Структура;
	
	Если Не ЗначениеЗаполнено(ID_дистрибьютора) Тогда
		коллекцияОшибок.Вставить(
			ЭтотОбъект.Метаданные().Реквизиты.ID_дистрибьютора.Имя,
			"Не заполнен код дистрибютора"
			);
			
	КонецЕсли;
	
	Для Каждого пара Из коллекцияОшибок Цикл 
		имяРеквизита = пара.Ключ;
		стрОшибка = пара.Значение;
		
		ОшибкаВЖурнал("ПроверкиПередВыгрузкой", стрОшибка);
			
		сообщ = Новый СообщениеПользователю;
		сообщ.Поле = имяРеквизита;
		сообщ.Текст = стрОшибка;
		сообщ.Сообщить();
		
		// В фоновом задании вызвать метод "Сообщить", чтобы в консоли можно было прочесть ошибки
		Если этоФоновое Тогда
			Сообщить(стрОшибка);
			
		КонецЕсли;
		
	КонецЦикла;
	
	проверкаПройдена = коллекцияОшибок.Количество() = 0;
	
	Возврат проверкаПройдена;
	
КонецФункции

Функция ТекстЗапроса(пИмяФайла)
	
	Если Ложь Тогда 
		
	ИначеЕсли пИмяФайла = ИмяФайла_cancellations() Тогда
		
		//Описание таблицы:		
		//Название столбца				Формат			Комментарий
		//id дистрибьютора				Integer			Код дистрибьютора в системе SPOT2D, указан в теле письма
		//Дата							Date			фактическая дата проведения операции в одном из указанных форматов: dd.mm.yy, dd-mm-yy, dd.m.yy, dd-mm-yyyy, dd.mm.yyyy, dd-m-yyyy, yyyy.mm.dd, yyyy-mm-dd, yyyy-m-dd, yyyy-m-d, yyyy-mm-d
		//Код продукта дистрибьютора	String (128)	Внутренний код продукта из УС дистрибьютора. 
		//Количество					Float			Количество в единицах измерения продукции. Для операций, уменьшающих остаток продукции, необходимо использовать отрицательные значения: - исходящие перемещения, - списание/недостача. Для операций, увеличивающих остаток, необходимо использовать положительные значения: - входящие перемещения, - излишек.
		//Номер документа				String (128)	Номер операции в учетной системе дистрибьютора
		
		Возврат
			"ВЫБРАТЬ
			|	ВЫРАЗИТЬ(Обороты.Субконто1 КАК Справочник.Номенклатура).Код КАК Код_продукта_дистрибьютора,
			|	НАЧАЛОПЕРИОДА(Обороты.Период, ДЕНЬ) КАК Дата,
			|	СУММА(Обороты.КоличествоОборотДт - Обороты.КоличествоОборотКт) КАК Количество,
			|	ВЫРАЗИТЬ(Обороты.Регистратор КАК Документ.КомплектацияНоменклатуры).Номер КАК Номер_документа
			|ПОМЕСТИТЬ комплектации
			|ИЗ
			|	РегистрБухгалтерии.Хозрасчетный.Обороты(
			|			&ДатаНачала,
			|			&ДатаОкончания,
			|			Регистратор,
			|			Счет = &Счет41_1,
			|			ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.Номенклатура),
			|			Субконто1 В
			|				(ВЫБРАТЬ
			|					о.Ссылка
			|				ИЗ
			|					отборНоменклатуры КАК о),
			|			КорСчет = &Счет41_1,
			|			) КАК Обороты
			|ГДЕ
			|	Обороты.Регистратор ССЫЛКА Документ.КомплектацияНоменклатуры
			|
			|СГРУППИРОВАТЬ ПО
			|	ВЫРАЗИТЬ(Обороты.Субконто1 КАК Справочник.Номенклатура).Код,
			|	НАЧАЛОПЕРИОДА(Обороты.Период, ДЕНЬ),
			|	ВЫРАЗИТЬ(Обороты.Регистратор КАК Документ.КомплектацияНоменклатуры).Номер
			|
			|ИНДЕКСИРОВАТЬ ПО
			|	Дата
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	&ID_дистрибьютора КАК id_дистрибьютора,
			|	ВЫРАЗИТЬ(перваяНоменклатура.Дата КАК ДАТА) КАК Дата,
			|	ВЫРАЗИТЬ(ЕСТЬNULL(комплектации.Код_продукта_дистрибьютора, перваяНоменклатура.Код_продукта_дистрибьютора) КАК СТРОКА(128)) КАК Код_продукта_дистрибьютора,
			|	ЕСТЬNULL(комплектации.Количество, 0) КАК Количество,
			|	ВЫРАЗИТЬ(ЕСТЬNULL(комплектации.Номер_документа, """") КАК СТРОКА(128)) КАК Номер_документа
			|ИЗ
			|	перваяНоменклатура КАК перваяНоменклатура
			|		ЛЕВОЕ СОЕДИНЕНИЕ комплектации КАК комплектации
			|		ПО перваяНоменклатура.Дата = комплектации.Дата
			|
			|УПОРЯДОЧИТЬ ПО
			|	Дата,
			|	Код_продукта_дистрибьютора";


	ИначеЕсли пИмяФайла = ИмяФайла_delivery() Тогда 
		// Данные об обороте продукции (отгрузки на ТТ, возвраты с ТТ, корректировочный СФ) 
		// загружаются на сайт ежедневно за период со вчера и минус 45 дней 
		// (например, если файлы формируются 15.02.2015, тогда delivery должен содержать информацию 
		// за период с 1.01.2015 по 14.02.2015 включительно).
		// В файл не должны попадать списания, перемещения, пересортица и другие операции инвентаризации.
		// Обратите внимание, что необходимо создать возможность задавать период выгружаемых данных, 
		// так как будут ситуации, когда нужно выгрузить информацию более чем за 45 дней (исторические данные).
		
		//Описание таблицы:
		//
		//Название заголовка			Формат			Комментарий
		//id дистрибьютора				Integer			Код дистрибьютора в системе SPOT 2D, указан в теле письма
		//Код клиента ERP				String (128)	Уникальный код связки кода клиента (контрагента) и кода торговой точки (точки доставки).  
		//													Детально описание см. в файле ttoptions.csv
		//Дата							Date			фактическая дата отгрузки или возврата в ТТ в одном из указанных форматов: 
		//													dd.mm.yy, dd-mm-yy, dd.m.yy, dd-mm-yyyy, dd.mm.yyyy, dd-m-yyyy, yyyy.mm.dd, yyyy-mm-dd, yyyy-m-dd, yyyy-m-d, yyyy-mm-d
		//Код продукта дистрибьютора	String (128)	Внутренний код продукта из УС дистрибьютора. 
		//Количество					Float			Размер отгрузки в единицах измерения продукции. Возврат со знаком минус.
		//Сумма отгрузки				Float			Стоимость всей отгрузки/возврата в национальной валюте без НДС. Сумма возврата со знаком минус.
		//Сумма отгрузки с НДС			Float			Стоимость всей отгрузки/возврата в национальной валюте c НДС. Сумма возврата со знаком минус.
		//Код ТА						String (64)		Код торгового представителя или агента (ТА) на которого зачислена продажа, 
		//													если к продаже не привязан ТА необходимо оставить поле пустым.
		//Номер расходной накладной		String (128)	Номер расходной или возвратной накладной из УС дистрибьютора. Пустые значения не допускаются.

		Возврат 
			"ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	клиенты.Клиент КАК Клиент,
			|	клиенты.КодТТ КАК КодТТ,
			|	клиенты.Код_клиента_ERP КАК Код_клиента_ERP
			|ПОМЕСТИТЬ клиентыРазличные
			|ИЗ
			|	клиенты КАК клиенты
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	&ID_дистрибьютора КАК id_дистрибьютора,
			|	клиенты.Код_клиента_ERP КАК Код_клиента_ERP,
			|	документыРеализации.Дата КАК Дата,
			|	реализованныеТовары.Код_продукта_дистрибьютора КАК Код_продукта_дистрибьютора,
			|	СУММА(реализованныеТовары.Количество) КАК Количество,
			|	СУММА(реализованныеТовары.Сумма_отгрузки) КАК Сумма_отгрузки,
			|	СУММА(реализованныеТовары.Сумма_отгрузки_с_НДС) КАК Сумма_отгрузки_с_НДС,
			|	"""" КАК Код_ТА,
			|	документыРеализации.Номер_расходной_накладной КАК Номер_расходной_накладной
			|ИЗ
			|	документыРеализации КАК документыРеализации
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ клиентыРазличные КАК клиенты
			|		ПО документыРеализации.Клиент = клиенты.Клиент
			|			И документыРеализации.КодТТ = клиенты.КодТТ
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ реализованныеТовары КАК реализованныеТовары
			|		ПО документыРеализации.Ссылка = реализованныеТовары.Ссылка
			|			И (реализованныеТовары.Номенклатура В
			|				(ВЫБРАТЬ
			|					отборНоменклатуры.Ссылка
			|				ИЗ
			|					отборНоменклатуры КАК отборНоменклатуры))
			|
			|СГРУППИРОВАТЬ ПО
			|	клиенты.Код_клиента_ERP,
			|	документыРеализации.Дата,
			|	реализованныеТовары.Код_продукта_дистрибьютора,
			|	документыРеализации.Номер_расходной_накладной
			|
			|УПОРЯДОЧИТЬ ПО
			|	Дата,
			|	Номер_расходной_накладной,
			|	Код_продукта_дистрибьютора" 
		
	ИначеЕсли пИмяФайла = ИмяФайла_receive() Тогда
		//Файл должен включать информацию о приходах товара от производителя, а также возвраты со склада дистрибьютора на склад производителя. 
		//
		//Данные о приходах и возвратах загружаются на сайт ежедневно за период со вчера и минус 45 дней.
		//Пример импорта: файлы выгружаются на сайт 15.02.2015, тогда receive должен содержать информацию за период с 1.01.2015 по14.02.2015 включительно.  
		//
		//Файл должен содержать 45 дат. Если в какой-либо день нет прихода, то необходимо включить эту дату в файл 
		// с нулевым значением в количестве по любому коду СКЮ из файла sku.csv.
		//
		//Описание таблицы:		
		//		
		//Название заголовка			Формат			Комментарий
		//id дистрибьютора				Integer			Код дистрибьютора в системе SPOT 2D, указан в теле письма
		//Дата							Date			фактическая дата прихода товара на склад дистрибьютора 
		//												или дата возврата на склад производителя в одном из указанных форматов: 
		//												dd.mm.yy, dd-mm-yy, dd.m.yy, dd-mm-yyyy, dd.mm.yyyy, dd-m-yyyy, yyyy.mm.dd, yyyy-mm-dd, yyyy-m-dd, yyyy-m-d, yyyy-mm-d
		//Код продукта дистрибьютора	String (128)	Внутренний код продукта из УС дистрибьютора. 
		//Количество					Float			Размер прихода или возврата в единицах измерения продукции. Возвраты со знаком минус.
		//Номер накладной				String (128)	Номер приходной накладной от производителя или номер возвратной накладной

		Возврат
			"ВЫБРАТЬ
			|	ВЫРАЗИТЬ(Обороты.Субконто1 КАК Справочник.Номенклатура).Код КАК Код_продукта_дистрибьютора,
			|	НАЧАЛОПЕРИОДА(Обороты.Период, ДЕНЬ) КАК Дата,
			|	СУММА(Обороты.КоличествоОборотДт) КАК Количество,
			|	ВЫРАЗИТЬ(Обороты.Регистратор КАК Документ.ПоступлениеТоваровУслуг).Номер КАК Номер_накладной
			|ПОМЕСТИТЬ поступления
			|ИЗ
			|	РегистрБухгалтерии.Хозрасчетный.Обороты(
			|			&ДатаНачала,
			|			&ДатаОкончания,
			|			Регистратор,
			|			Счет = &Счет41_1,
			|			ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.Номенклатура),
			|			Субконто1 В
			|				(ВЫБРАТЬ
			|					о.Ссылка
			|				ИЗ
			|					отборНоменклатуры КАК о),
			|			КорСчет <> &Счет41_1,
			|			) КАК Обороты
			|ГДЕ
			|	Обороты.КоличествоОборотДт <> 0
			|	И НЕ Обороты.Регистратор ССЫЛКА Документ.ВозвратТоваровОтПокупателя
			|	И НЕ Обороты.Регистратор ССЫЛКА Документ.КомплектацияНоменклатуры
			|
			|СГРУППИРОВАТЬ ПО
			|	ВЫРАЗИТЬ(Обороты.Субконто1 КАК Справочник.Номенклатура).Код,
			|	НАЧАЛОПЕРИОДА(Обороты.Период, ДЕНЬ),
			|	ВЫРАЗИТЬ(Обороты.Регистратор КАК Документ.ПоступлениеТоваровУслуг).Номер
			|
			|ИНДЕКСИРОВАТЬ ПО
			|	Дата
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	&ID_дистрибьютора КАК id_дистрибьютора,
			|	ВЫРАЗИТЬ(перваяНоменклатура.Дата КАК ДАТА) КАК Дата,
			|	ВЫРАЗИТЬ(ЕСТЬNULL(поступления.Код_продукта_дистрибьютора, перваяНоменклатура.Код_продукта_дистрибьютора) КАК СТРОКА(128)) КАК Код_продукта_дистрибьютора,
			|	ЕСТЬNULL(поступления.Количество, 0) КАК Количество,
			|	ВЫРАЗИТЬ(ЕСТЬNULL(поступления.Номер_накладной, """") КАК СТРОКА(128)) КАК Номер_накладной
			|ИЗ
			|	перваяНоменклатура КАК перваяНоменклатура
			|		ЛЕВОЕ СОЕДИНЕНИЕ поступления КАК поступления
			|		ПО перваяНоменклатура.Дата = поступления.Дата
			|
			|УПОРЯДОЧИТЬ ПО
			|	Дата,
			|	Код_продукта_дистрибьютора";
		
	ИначеЕсли пИмяФайла = ИмяФайла_sku() Тогда
		// Файл sku.csv должен содержать список продукции производителя, по которому настраивается выгрузка данных.
		// Обратите внимание, что для новых продуктов запрещено использовать коды старых продуктов.
		//
		//Описание таблицы:
		//
		//Название заголовка			Формат			Комментарий
		//id дистрибьютора				Integer			Код дистрибьютора в системе SPOT 2D, указан в теле письма
		//Код продукта дистрибьютора	String (128)	Внутренний код продукта из УС дистрибьютора.  Коды продуктов должны быть уникальными.
		//Название продукта				String (128)	Название продукта в Вашей учетной системе
		//Штрихкод						String (128)	Штрихкод товара производителя (EAN)
		//Код продукта производителя	String (128)	Код товара Производителя (если ведется учет в кодах производителя)
		//id единицы измерения продукта	Integer			Id единицы измерения производителя: штуки –1, ящики – 2, килограммы – 3, дополнительная единица -4

		Возврат
			"ВЫБРАТЬ
			|	ЕИ.Ссылка КАК Ссылка,
			|	ВЫРАЗИТЬ(ВЫБОР
			|			КОГДА ЕИ.Код = ""796"" // шт
			|				ТОГДА 1
			|			КОГДА ЕИ.Код = ""778"" // упак
			|				ТОГДА 2
			|			КОГДА ЕИ.Код = ""166"" // кг
			|				ТОГДА 3
			|			ИНАЧЕ 2 // все остальное считаем ящиками
			|		КОНЕЦ КАК ЧИСЛО(1, 0)) КАК id_единицы_измерения_продукта
			|ПОМЕСТИТЬ еи
			|ИЗ
			|	Справочник.КлассификаторЕдиницИзмерения КАК ЕИ
			|
			|ИНДЕКСИРОВАТЬ ПО
			|	ЕИ.Ссылка
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	&ID_дистрибьютора КАК id_дистрибьютора,
			|	Номенклатура.Код КАК Код_продукта_дистрибьютора,
			|	Номенклатура.Наименование КАК Название_продукта,
			|	"""" КАК Штрихкод,
			|	"""" КАК Код_продукта_производителя,
			|	ВЫРАЗИТЬ(ЕСТЬNULL(еи.id_единицы_измерения_продукта, 0) КАК ЧИСЛО(1, 0)) КАК id_единицы_измерения_продукта
			|ИЗ
			|	отборНоменклатуры КАК отбор
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК Номенклатура
			|		ПО отбор.Ссылка = Номенклатура.Ссылка
			|		ЛЕВОЕ СОЕДИНЕНИЕ еи КАК еи
			|		ПО (Номенклатура.ЕдиницаИзмерения = еи.Ссылка)" 
		
	ИначеЕсли пИмяФайла = ИмяФайла_stocks() Тогда
		// Файл должен содержать 45 дат. 
		// Если в какой-либо день на остатках нет товара, то необходимо включить эту дату в файл с нулевым значением остатка в количестве 
		// по любому коду СКЮ из файла Данные об остатках загружаются на сайт ежедневно за период со вчера и минус 45 дней. 
		// В файле необходимо отображать ежедневные остатки на конец дня по каждому продукту за период 45 дней.
		//
		//Отрицательные остатки необходимо исключить из файла.
		//
		//Пример: файлы выгружаются на сайт 15.02.2015, тогда stocks должен содержать информацию на конец каждого дня 
		// за период с 1.01.2015 по 14.02.2015 включительно.
		// 
		//Описание таблицы:
		//
		//Название заголовка			Формат			Комментарий
		//id дистрибьютора				Integer			Код дистрибьютора в системе SPOT 2D, указан в теле письма
		//Дата							Date			фактическая дата остатка в одном из указанных форматов: dd.mm.yy, dd-mm-yy, dd.m.yy, dd-mm-yyyy, dd.mm.yyyy, dd-m-yyyy, yyyy.mm.dd, yyyy-mm-dd, yyyy-m-dd, yyyy-m-d, yyyy-mm-d
		//Код продукта дистрибьютора	String (128)	Внутренний код продукта из УС дистрибьютора. 
		//Количество					Float			Размер остатков на конец каждого дня в единицах измерения продукции. Отрицательные остатки выгружать запрещено.

		Возврат 
			"ВЫБРАТЬ
			|	ВЫРАЗИТЬ(отборНоменклатуры.Ссылка КАК Справочник.Номенклатура) КАК Номенклатура,
			|	ЕСТЬNULL(Остатки.КоличествоОстатокДт, 0) КАК Количество
			|ПОМЕСТИТЬ остаткиНачальные
			|ИЗ
			|	отборНоменклатуры КАК отборНоменклатуры
			|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Хозрасчетный.Остатки(
			|				&ДатаНачала,
			|				Счет = &Счет41_1,
			|				,
			|				Субконто1 В
			|					(ВЫБРАТЬ
			|						отборНоменклатуры.Ссылка
			|					ИЗ
			|						отборНоменклатуры КАК отборНоменклатуры)) КАК Остатки
			|		ПО отборНоменклатуры.Ссылка = Остатки.Субконто1
			|
			|ИНДЕКСИРОВАТЬ ПО
			|	Номенклатура
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ВЫРАЗИТЬ(Обороты.Субконто1 КАК Справочник.Номенклатура) КАК Номенклатура,
			|	Обороты.Период КАК Дата,
			|	Обороты.КоличествоОборот КАК Количество
			|ПОМЕСТИТЬ обороты
			|ИЗ
			|	РегистрБухгалтерии.Хозрасчетный.Обороты(
			|			&ДатаНачала,
			|			&ДатаОкончания,
			|			День,
			|			Счет = &Счет41_1,
			|			ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.Номенклатура),
			|			Субконто1 В
			|				(ВЫБРАТЬ
			|					о.Ссылка
			|				ИЗ
			|					отборНоменклатуры КАК о),
			|			,
			|			) КАК Обороты
			|
			|ИНДЕКСИРОВАТЬ ПО
			|	Номенклатура,
			|	Дата
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	матрица.Номенклатура КАК Номенклатура,
			|	матрица.Дата КАК Дата,
			|	СУММА(обороты.Количество) КАК Количество
			|ПОМЕСТИТЬ оборотыНарастающие
			|ИЗ
			|	матрица КАК матрица
			|		ЛЕВОЕ СОЕДИНЕНИЕ обороты КАК обороты
			|		ПО матрица.Номенклатура = обороты.Номенклатура
			|			И матрица.Дата >= обороты.Дата
			|
			|СГРУППИРОВАТЬ ПО
			|	матрица.Номенклатура,
			|	матрица.Дата
			|
			|ИНДЕКСИРОВАТЬ ПО
			|	Номенклатура
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ЕСТЬNULL(оборотыНарастающие.Номенклатура, остаткиНачальные.Номенклатура) КАК Номенклатура,
			|	ЕСТЬNULL(оборотыНарастающие.Дата, &ДатаНачала) КАК Дата,
			|	СУММА(ЕСТЬNULL(оборотыНарастающие.Количество, 0) + ЕСТЬNULL(остаткиНачальные.Количество, 0)) КАК Количество
			|ПОМЕСТИТЬ остатки
			|ИЗ
			|	оборотыНарастающие КАК оборотыНарастающие
			|		ПОЛНОЕ СОЕДИНЕНИЕ остаткиНачальные КАК остаткиНачальные
			|		ПО оборотыНарастающие.Номенклатура = остаткиНачальные.Номенклатура
			|
			|СГРУППИРОВАТЬ ПО
			|	ЕСТЬNULL(оборотыНарастающие.Номенклатура, остаткиНачальные.Номенклатура),
			|	ЕСТЬNULL(оборотыНарастающие.Дата, &ДатаНачала)
			|
			|ИМЕЮЩИЕ
			|	СУММА(ЕСТЬNULL(оборотыНарастающие.Количество, 0) + ЕСТЬNULL(остаткиНачальные.Количество, 0)) > 0
			|
			|ИНДЕКСИРОВАТЬ ПО
			|	Номенклатура,
			|	Дата
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	&ID_дистрибьютора КАК id_дистрибьютора,
			|	ВЫРАЗИТЬ(матрица.Дата КАК ДАТА) КАК Дата,
			|	ВЫРАЗИТЬ(матрица.Код_продукта_дистрибьютора КАК СТРОКА(128)) КАК Код_продукта_дистрибьютора,
			|	ЕСТЬNULL(остатки.Количество, 0) КАК Количество
			|ИЗ
			|	матрица КАК матрица
			|		ЛЕВОЕ СОЕДИНЕНИЕ остатки КАК остатки
			|		ПО матрица.Номенклатура = остатки.Номенклатура
			|			И матрица.Дата = остатки.Дата
			|			И (остатки.Количество > 0)
			|ГДЕ
			|	ЕСТЬNULL(остатки.Количество, 0) <> 0
			|
			|УПОРЯДОЧИТЬ ПО
			|	Код_продукта_дистрибьютора,
			|	Дата";
		
		
	ИначеЕсли пИмяФайла = ИмяФайла_ta() Тогда
		НеРеализовано();
		
		//Описание таблицы:
		//
		//Название заголовка	Формат			Комментарий
		//id дистрибьютора		Integer			Код дистрибьютора в системе SPOT 2D, указан в теле письма
		//Код ТА				String (64)		Уникальный код торгового представителя/агента(ТА) в системе дистрибьютора
		//Имя ТА				String (128)	Уникальное название ТА

		Возврат
			"ВЫБРАТЬ
			|	&ID_дистрибьютора КАК id_дистрибьютора,
			|	т.Код_ТА КАК Код_ТА,
			|	т.Имя_ТА КАК Имя_ТА
			|ИЗ
			|	&Т КАК т" 
		
	ИначеЕсли пИмяФайла = ИмяФайла_ttoptions() Тогда
		
		//Описание таблицы:
		//
		//Название заголовка	Формат (размер)	Комментарий
		//id дистрибьютора		Integer			Код дистрибьютора в системе SPOT 2D, указан в теле письма.
		//Код клиента ERP		String (128)	Уникальный код связки кода клиента (контрагента) и кода торговой точки (точки доставки).
		//											Пример правильного кода XXXXXXX#YYYYYY Где,  
		//												XXXXXXX – код клиента (контрагента) 
		//												YYYYYY – код торговой точки (точки доставки) 
		//												# - разделитель # без пробелов. Например, 2599#3727 
		//											Если в системе дистрибутора есть только один из кодов (клиента или торговой точки доставки), 
		//											то формат связки кода должен выгружаться в виде XXXXXXX#XXXXXXX или YYYYYY#YYYYYY
		//Название клиента		String (128)	Юридическое название клиента (контрагента).Например, Абишева А.М. ИП или Ашан Ритейл
		//Адрес клиента			String (256)	Юридический адрес клиента (контрагента).   Необходимый формат адресной строки: Индекс, Страна, Область, Населенный пункт, Улица, номер дома Если какой-либо части адреса нет, то ее необходимо пропустить, не нарушая порядок следования. Допускаются общепринятые сокращение обл, р-н, ул, д. и тд.  Пример правильного формата адресной строки: 142400, Казахстан, Актюбинская обл, г Ногинск, ул Соборная, д 12 Или 142400, Казахстан, Актюбинская обл, Ногинск, ул Соборная, 12  Если в базе не заведен юр.адрес - выгружать фразу "Юр.адрес не заведен"
		//Название ТТ			String (128)	Юр. название клиента (контрагента) + название торговой точки доставки Например: Абишева А.М. ИП магазин «Людмила» или Ашан Ритейл, гипермаркет №3
		//Адрес ТТ				String (256)	Фактический адрес точки доставки (адрес грузополучателя).  Необходимый формат адресной строки: Индекс, Страна, Область, Населенный пункт, Улица, номер дома Если какой-либо части адреса нет, то ее необходимо пропустить, не нарушая порядок следования. Допускаются общепринятые сокращение обл, р-н, ул, д. и тд.  Пример правильного формата адресной строки: 142400, Казахстан, Актюбинская обл, г Ногинск, ул Соборная, д 12 Или 142400, Казахстан, Актюбинская обл, Ногинск, ул Соборная, 12  Если в базе не заведен адрес точки доставки - выгружать фразу "Самовывоз" или адрес склада.
		//Код ИНН				Integer			Идентификационный номер налогоплательщика - клиента (контрагента). Должен содержать минимум 9 цифр, максимум -12. В случае, если кода ИНН нет в Вашей системе, ячейку можно оставить пустой.
		//Сегмент				String (256) 	Необходимо указать к какому сегменту относиться ТТ: Ритейл, Хорека, Пищевые производства, Оптовый канал, другое (все что не входит в список).Если данная информация не ведется в учетной системе, поле можно оставить пустым. 

		Возврат
			"ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	&ID_дистрибьютора КАК id_дистрибьютора,
			|	клиенты.Код_клиента_ERP КАК Код_клиента_ERP,
			|	клиенты.Название_клиента КАК Название_клиента,
			|	ВЫБОР
			|		КОГДА клиенты.Адрес_клиента <> """"
			|			ТОГДА клиенты.Адрес_клиента
			|		ИНАЧЕ ""Юр. адрес не заведен""
			|	КОНЕЦ КАК Адрес_клиента,
			|	клиенты.Название_клиента КАК Название_ТТ,
			|	ВЫБОР
			|		КОГДА клиенты.Адрес_клиента <> """"
			|			ТОГДА клиенты.Адрес_клиента
			|		ИНАЧЕ ""Самовывоз""
			|	КОНЕЦ КАК Адрес_ТТ,
			|	клиенты.Код_ИНН КАК Код_ИНН,
			|	"""" КАК Сегмент
			|ИЗ
			|	документыРеализации КАК рту
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ клиенты КАК клиенты
			|		ПО рту.Клиент = клиенты.Клиент" 
		
	КонецЕсли; // Если пИмяФайла = ...
	
КонецФункции

Функция ОтправитьВSpot2D(пИмяФайла, пСодержимоеФайла) 

	граница = СтрЗаменить(Строка(Новый УникальныйИдентификатор()), "-", "");
	стрЗапрос = 
		СтрШаблон(
			"--%1
			|Content-Disposition: form-data; name=""__login""
			|
			|%2
			|--%1
			|Content-Disposition: form-data; name=""__password""
			|
			|%3
			|--%1
			|Content-Disposition: form-data; name=""__did""
			|
			|%4
			|--%1
			|Content-Disposition: form-data; name=""ufile""; filename=""%5.csv""
			|Content-Type: text/plain
			|
			|%6
			|--%1--",
			граница, // %1 
			Пользователь, // %2
			Пароль, // %3
			ID_дистрибьютора, // %4
			пИмяФайла, // %5
			пСодержимоеФайла, // %6
			);
	
	заголовки = Новый Соответствие();
	заголовки.Вставить("Content-Type","multipart/form-data; boundary=" + граница);
	HTTPЗапрос = Новый HTTPЗапрос(АдресРесурса(), заголовки);
	HTTPЗапрос.УстановитьТелоИзСтроки(стрЗапрос, КодировкаТекста.UTF8, ИспользованиеByteOrderMark.НеИспользовать); 

	соединение = Новый HTTPСоединение(АдресСервера());
	отправлено = Ложь;
	
	событие = "ОтправитьВSpot2D." + пИмяФайла;

	Попытка 
		ответ = соединение.ОтправитьДляОбработки(HTTPЗапрос); 
		отправлено = ответ.КодСостояния >= 200 И ответ.КодСостояния < 300;
		
		ВЖурнал(событие, "ответ.КодСостояния = " + ответ.КодСостояния);

	Исключение 
		отправлено = Ложь; 
		
		представлениеОшибки = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ОшибкаВЖурнал(событие, представлениеОшибки);
		
		ОбщегоНазначения.СообщитьПользователю(
			СтрШаблон(
				"Ошибка при отправке файла по причине
				|%1",
				ОбработкаОшибок.КраткоеПредставлениеОшибки(ИнформацияОбОшибке())
				)
			);
		
	КонецПопытки;
	
	Если отправлено Тогда
		ВЖурнал(событие, пСодержимоеФайла);
		
	Иначе
		ОшибкаВЖурнал(событие, "Сообщение не отправлено, ошибки смотрите в журнале регистрации выше");
		
	КонецЕсли;
	
	Возврат отправлено;
	
КонецФункции

Процедура ВЖурнал(пСобытие = "", пОписаниесобытия, пУровеньЖурнала = Неопределено)
	
	Если ТипЗнч(пУровеньЖурнала) <> Тип("УровеньЖурналаРегистрации") Тогда
		пУровеньЖурнала = УровеньЖурналаРегистрации.Информация;
		
	КонецЕсли; 
	
	ЗаписьЖурналаРегистрации(
		ИмяСобытияЖурналаРегистрации(пСобытие),
		пУровеньЖурнала,
		ЭтотОбъект.Метаданные(),
		, // Данные
		пОписаниесобытия,
		РежимТранзакцииЗаписиЖурналаРегистрации.Независимая
		);
	
	// В фоновом задании вызвать метод "Сообщить", чтобы информацию можно было достать методом "ПолучитьСообщенияПользователю"
	Если ЭтоФоновоеЗадание() Тогда
		стрСообщение = 
			СтрШаблон(
				"%1 %2: %3",
				Формат(ТекущаяДатаСеанса(), "ДЛФ=T"), // %1
				пСобытие, // %2
				пОписаниесобытия // %3
				);
			
		Сообщить(стрСообщение);
		
	КонецЕсли;

КонецПроцедуры 

Процедура ОшибкаВЖурнал(пСобытие, пОписаниеОшибки)
	
	ВЖурнал(пСобытие, пОписаниеОшибки, УровеньЖурналаРегистрации.Ошибка);
	
КонецПроцедуры

#КонецОбласти // Выгрузка

#Область ВолшебныеЧислаДвижка

Функция АдресРесурса()
	
	Возврат "/upload/auto";
	
КонецФункции

Функция АдресСервера()
	
	Возврат "osq.mdata.report";
	
КонецФункции

Функция ИмяКоманды_ВыгрузитьВФоне()
	
	Возврат "ВыгрузитьВФоне";
	
КонецФункции  

Функция ИмяСобытияЖурналаРегистрации(пДополнительноеИмя = "") 
	
	имяСобытия = ЭтотОбъект.Метаданные().Имя;
	
	Если Не ПустаяСтрока(пДополнительноеИмя) Тогда
		имяСобытия = СтрШаблон("%1.%2", имяСобытия, пДополнительноеИмя);
		
	КонецЕсли;
	
	Возврат имяСобытия;
	
КонецФункции  

Функция КлючОбъекта()
	
	Возврат ЭтотОбъект.Метаданные().Имя;
	
КонецФункции

Функция КлючНастроек() 
	
	Возврат "СтруктураНастроек";
	
КонецФункции 

#КонецОбласти // ВолшебныеЧислаДвижка

#Область ВолшебныеЧислаДанных

Функция ИмяФайла_cancellations()
	
	Возврат "cancellations";
	
КонецФункции

Функция ИмяФайла_delivery()
	
	Возврат "delivery";
	
КонецФункции

Функция ИмяФайла_receive()
	
	Возврат "receive";
	
КонецФункции

Функция ИмяФайла_sku()
	
	Возврат "sku";
	
КонецФункции

Функция ИмяФайла_stocks()
	
	Возврат "stocks";
	
КонецФункции

Функция ИмяФайла_ta()
	
	Возврат "ta";
	
КонецФункции

Функция ИмяФайла_ttoptions()
	
	Возврат "ttoptions";
	
КонецФункции

Процедура ПериодВыгрузки_Установить(Знач пДата = Неопределено, пОтДатыОкончания = Истина) Экспорт
	
	ПериодВыгрузки.Вариант = ВариантСтандартногоПериода.ПроизвольныйПериод;
	
	Если пДата = Неопределено Тогда 
		Если пОтДатыОкончания Тогда
			пДата = ТекущаяДатаСеанса() - 24*3600;
			
		Иначе
			пДата = ТекущаяДатаСеанса();
			
		КонецЕсли;
		
	КонецЕсли; 
	
	// Текущий день не выгружаем, в файле должно быть 45 дат, потому берем 46 дней
	период45Дней = 24*3600*45;
	
	Если пОтДатыОкончания Тогда
		ПериодВыгрузки.ДатаОкончания = НачалоДня(пДата);
		ПериодВыгрузки.ДатаНачала = НачалоДня(пДата - период45Дней - 1); // -45 дней
		
	Иначе
		ПериодВыгрузки.ДатаНачала = НачалоДня(пДата);
		ПериодВыгрузки.ДатаОкончания = КонецДня(пДата + период45Дней + 1);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти // ВолшебныеЧислаДанных

#Область общ 

Функция НеРеализовано()		ВызватьИсключение "Не реализовано";	КонецФункции 
// @ Клиент-сервер
//
// На основании процедуры модуля унсКлиент.ДобавитьЭлементОтбораКомпоновкиДанныхВКоллекцию
// Из работы Максима Зайцеа https://infostart.ru/1c/tools/93883/ 
// "Подсистема сохранения и восстановления настроек динамических списков"
Функция ОтборСКД_Копировать(пОтборПриемник, пОтборИсточник) Экспорт 
	
	пОтборПриемник.Элементы.Очистить(); 
	
	// Перенести элементы отбора в массив, чтобы с его помощью избавиться от рекурсивного обхода
	массивИсточника = Новый Массив;
	Для Каждого элементИсточника Из пОтборИсточник.Элементы Цикл
		// Соответствие хранит элемент и его родителя
		местоВставки = пОтборПриемник.Элементы;
		коллекцияЭлементИМестоВставки = ОтборСКД_коллекцияЭлементИМестоВставки(элементИсточника, местоВставки);
		массивИсточника.Добавить(коллекцияЭлементИМестоВставки);
		
	КонецЦикла;
	элементИсточника = Неопределено;
	
	Для Каждого коллекцияЭлементИМестоВставки Из массивИсточника Цикл
		Для Каждого пара Из коллекцияЭлементИМестоВставки Цикл
			элементИсточника = пара.Ключ;
			местоВставки = пара.Значение; 
			Прервать;
			
		КонецЦикла;
		пара = Неопределено;
		
		Если ТипЗнч(элементИсточника) = Тип("ГруппаЭлементовОтбораКомпоновкиДанных") Тогда
			новаяГруппаПриемника = местоВставки.Добавить(Тип("ГруппаЭлементовОтбораКомпоновкиДанных") );
			ЗаполнитьЗначенияСвойств(новаяГруппаПриемника, элементИсточника);
			Для Каждого элементПотомок Из элементИсточника.Элементы Цикл
				коллекцияПотомокИРодитель = ОтборСКД_коллекцияЭлементИМестоВставки(элементПотомок, новаяГруппаПриемника.Элементы);
				массивИсточника.Добавить(коллекцияПотомокИРодитель);
				
			КонецЦикла;
			элементПотомок = Неопределено;
			
		Иначе // добавляем элемент отбора 
			новыйЭлементПриемника = местоВставки.Добавить( Тип("ЭлементОтбораКомпоновкиДанных") );
			ЗаполнитьЗначенияСвойств(новыйЭлементПриемника, элементИсточника );
			
		КонецЕсли;

	КонецЦикла; 
	коллекцияЭлементИМестоВставки = Неопределено;
	
КонецФункции

// Вспомогательная функция для методаОтборСКДКопировать
Функция ОтборСКД_коллекцияЭлементИМестоВставки(пЭлемент, пРодительЭлемента)
	
	описаниеЭлемента = Новый Соответствие;
	описаниеЭлемента.Вставить(пЭлемент, пРодительЭлемента);
	
	Возврат описаниеЭлемента;
	
КонецФункции

// Функция предназначена для вывода СКД в таблицу значений
// Используется, например, для использования отчета как источника данных
//
// Параметры:
//  пСхемаКомпоновкиДанных	 - СхемаКомпоновкиДанных - Схема, из которой необходимо получить таблицу значений
//  пНастройкиКД			 - НастройкиСхемыКомпоновкиДанных - суть в названии
// 
// Возвращаемое значение:
//  ТаблицаЗначений - результат компоновки СКД
//
Функция СКД_в_ТЗ(пСхемаКомпоновкиДанных, пНастройкиКД) Экспорт
	
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	МакетКомпоновки = КомпоновщикМакета.Выполнить(пСхемаКомпоновкиДанных, пНастройкиКД,,,Тип("ГенераторМакетаКомпоновкиДанныхДляКоллекцииЗначений"));
		
	ПроцессорКомпоновкиДанных = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновкиДанных.Инициализировать(МакетКомпоновки);
	
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВКоллекциюЗначений;
	тзРезультат = Новый ТаблицаЗначений;
	ПроцессорВывода.УстановитьОбъект(тзРезультат);
	ПроцессорВывода.Вывести(ПроцессорКомпоновкиДанных);
	
	Возврат тзРезультат;
	
КонецФункции

// ПреобразоватьТЗвТекстCSV() экспортирует данные ТЗ в текст в формате CSV
//
// Параметры:
//  пТЗ			 - 	ТаблицаЗначений - Обязательный, таблица значений, которую необходимо преобразовать в CSV
//  Разделитель	 - 	Строка - необязательный, разделитель полей CSV, по умолчанию ';'
//  пПараметры	 - 	Структура - необязательный, дополнительный параметры процедуры 
//					Возможные значения:
//						ВыводитьИменаКолонок - булево, признак необходимости в первой строке вывести имена колонок, по умолчанию Истина
//						ВосстановитьПробелыВИменахКолонок - булево, заменить символв '_' в именах колонок на пробел, по умолчанию Ложь;
//						ФорматДат - строка, форматная строка, которя будет использована для преобразования дат;
//
// Возвращаемое значение:
//  Строка - текст файла CSV
//
// Исходная процедура взята здесь: https://helpf.pro/faq8/view/1818.html
// 
Функция ТЗ_в_CSV(пТЗ, пРазделитель = ";", пПараметры = Неопределено) Экспорт
	
	#Область Инициализация
	
	Если ТипЗнч(пПараметры) <> Тип("Структура") Тогда
		пПараметры = Новый Структура;
		
	КонецЕсли;
	
	выводитьИменаКолонок = Истина;
	Если пПараметры.Свойство("ВыводитьИменаКолонок") Тогда
		выводитьИменаКолонок = пПараметры.ВыводитьИменаКолонок; 
		
	КонецЕсли;
	
	восстановитьПробелыВИменахКолонок = Ложь;
	Если пПараметры.Свойство("ВосстановитьПробелыВИменахКолонок") Тогда
		восстановитьПробелыВИменахКолонок = пПараметры.ВосстановитьПробелыВИменахКолонок;
		
	КонецЕсли; 
	
	форматДат = "";
	пПараметры.Свойство("ФорматДат", форматДат);
	
	#КонецОбласти // Инициализация
	
	массивСтрокCSV = Новый Массив;
		
	Если выводитьИменаКолонок Тогда
		//Если нужно выгружать наименование колонок Выгружаем
		именаКолонок = "";
		Для Каждого колонка Из пТЗ.Колонки Цикл
			имяКолонки = СокрЛП(колонка.Имя);
			Если восстановитьПробелыВИменахКолонок Тогда
				имяКолонки = СтрЗаменить(имяКолонки, "_", " ");
				
			КонецЕсли;
			
			именаКолонок = именаКолонок + имяКолонки + пРазделитель;
			
		КонецЦикла;
		колонка = Неопределено;
		
		именаКолонок = Лев(именаКолонок, СтрДлина(именаКолонок)-1);
		
		массивСтрокCSV.Добавить(именаКолонок);
		
	КонецЕсли; // Если выводитьИменаКолонко
	
	Для Каждого рядТЗ Из пТЗ Цикл
		массивПолейCSV = Новый Массив;
		
		Для Каждого колонка Из пТЗ.Колонки Цикл
			полеCSV = рядТЗ[колонка.Имя];
			
			Если ТипЗнч(полеCSV) = Тип("Дата") Тогда
				Если ЗначениеЗаполнено(форматДат) Тогда
					полеCSV = Формат(полеCSV, форматДат);
					
				Иначе
					полеCSV = XMLСтрока(полеCSV);
					
				КонецЕсли; 
				
			ИначеЕсли ТипЗнч(полеCSV) = Тип("Число") Тогда
				полеCSV = Формат(полеCSV, "ЧН=0; ЧГ=0");
				
			КонецЕсли;
			
			//по правилам CSV если поле содержит двойные ковычки они должны повторятся дважды
			Если Найти(полеCSV,"""") Тогда
				полеCSV = СтрЗаменить(полеCSV,"""","""""");
				
			КонецЕсли;
			
			//по правилам CSV если поле содержит перенос строки или запятую оно должно заключатся в двойные кавычки
			Если Найти(полеCSV, пРазделитель) ИЛИ Найти(полеCSV, Символы.ПС) ИЛИ Найти(полеCSV, """") Тогда
				полеCSV = """" + полеCSV + """";
				
			КонецЕсли;

			массивПолейCSV.Добавить(полеCSV);
			
		КонецЦикла; 
		колонка = Неопределено;
		
		строкаCSV = СтрСоединить(массивПолейCSV, пРазделитель);;
		
		массивСтрокCSV.Добавить(строкаCSV);
		
	КонецЦикла;
	рядТЗ = Неопределено; 
	
	стрCSV = СтрСоединить(массивСтрокCSV, Символы.ПС);

	Возврат стрCSV;

КонецФункции

// @ В Кэш 
Функция ЭтоФоновоеЗадание()
	
	текущийСеанс = ПолучитьТекущийСеансИнформационнойБазы();
	
	этоФоновое = текущийСеанс.ПолучитьФоновоеЗадание() <> Неопределено;
	
	Возврат этоФоновое;
	
КонецФункции

#КонецОбласти // общ
